#include "shader_platform.sl"
#include "raytrace.sl"

RaytracingAccelerationStructure Scene : register(t0, space0);
RWTexture2D<vec4> RenderTarget : register(u0);
ConstantBuffer<RayGenConstantBuffer> g_rayGenCB : register(b0);

struct RayPayload
{
    vec4 color;
};

bool IsInsideViewport(vec2 p, Viewport viewport)
{
    return (p.x >= viewport.left && p.x <= viewport.right)
        && (p.y >= viewport.top && p.y <= viewport.bottom);
}

//[shader("raygeneration")]
void MyRaygenShader()
{
    uint3 ray_index=DispatchRaysIndex();
    uint3 ray_dim=DispatchRaysDimensions();
    vec2 lerpValues = (vec2)ray_index.xy / (vec2)ray_dim.xy;
     
    // Orthographic projection since we're raytracing in screen space.
    vec3 rayDir = float3(0, 0, 1);
    vec3 origin = float3(
        lerp(g_rayGenCB.viewport.left, g_rayGenCB.viewport.right, lerpValues.x),
        lerp(g_rayGenCB.viewport.top, g_rayGenCB.viewport.bottom, lerpValues.y),
        0.0f);

    if (IsInsideViewport(origin.xy, g_rayGenCB.stencil))
    {
        // Trace the ray.
        // Set the ray's extents.
        RayDesc ray;
        ray.Origin = origin;
        ray.Direction = rayDir;
        // Set TMin to a non-zero small value to avoid aliasing issues due to floating - point errors.
        // TMin should be kept small to prevent missing geometry at close contact areas.
        ray.TMin = 0.001;
        ray.TMax = 10000.0;
        RayPayload payload = { float4(0, 0, 0, 0) };
        TraceRay(Scene, RAY_FLAG_CULL_BACK_FACING_TRIANGLES, ~0, 0, 1, 0, ray, payload);

        // Write the raytraced color to the output texture.
        RenderTarget[ray_index.xy] = payload.color;
    }
    else
    {
        // Render interpolated DispatchRaysIndex outside the stencil window
        RenderTarget[ray_index.xy] = float4(lerpValues, 0, 1);
    }
}

//[shader("closesthit")]
void MyClosestHitShader(inout RayPayload payload, in BuiltInTriangleIntersectionAttributes attr)
{
    float3 barycentrics = float3(2 - attr.barycentrics.x - attr.barycentrics.y, attr.barycentrics.x, attr.barycentrics.y);
    payload.color = float4(barycentrics, 1);
}

//[shader("miss")]
void MyMissShader(inout RayPayload payload)
{
    payload.color = float4(0, 0, 1, 1);
}


technique raytrace
{
    pass raytrace
    {
		SetRayGenerationShader(CompileShader(lib_6_3,MyRaygenShader()));
		SetClosestHitShader(CompileShader(lib_6_3,MyClosestHitShader()));
		SetMissShader(CompileShader(lib_6_3,MyMissShader()));
    }
}